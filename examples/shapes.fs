INCLUDE ../forth-struct.fs

STRUCTURE:
  CELLFLD FIELDNAME ELLIPSE.RMINOR
  CELLFLD FIELDNAME ELLIPSE.RMAJOR
 STRUCTSIZE ELLIPSE.SIZE
 STRUCTALIGN ELLIPSE.ALIGN
 STRUCTEMBED ELLIPSE.FLD
;STRUCTURE

( rminor rmajor ellipse -- )
: ELLIPSE.INIT TUCK ELLIPSE.RMAJOR ! ELLIPSE.RMINOR ! ;

( ellipse -- )
: ELLIPSE.PRINT
	DUP ." Ellipse, R = " ELLIPSE.RMAJOR @ . ." , r = " ELLIPSE.RMINOR @ . ;

STRUCTURE:
  CELLFLD 3 ARRAYFLD FIELDNAME TRIANGLE.SIDES
 STRUCTSIZE TRIANGLE.SIZE
 STRUCTALIGN TRIANGLE.ALIGN
 STRUCTEMBED TRIANGLE.FLD
;STRUCTURE

( side1 side2 side3 triangle -- )
: TRIANGLE.INIT TRIANGLE.SIDES 3 0 DO TUCK ! CELL+ LOOP DROP ;
: TRIANGLE.NEXTSIDE ." , " DUP @ . CELL+ ;
( triangle -- )
: TRIANGLE.PRINT
	." Triangle" TRIANGLE.SIDES 3 0 DO TRIANGLE.NEXTSIDE LOOP DROP ;

STRUCTURE:
  CELLFLD FIELDNAME SQUARE.SIDE
 STRUCTSIZE SQUARE.SIZE
 STRUCTALIGN SQUARE.ALIGN
 STRUCTEMBED SQUARE.FLD
;STRUCTURE

( side square -- )
: SQUARE.INIT SQUARE.SIDE ! ;
( square -- )
: SQUARE.PRINT DUP ." Square, " SQUARE.SIDE @ . ." x " SQUARE.SIDE @ . ;

STRUCTURE:
    ELLIPSE.FLD
    TRIANGLE.FLD
    SQUARE.FLD
  3 UNIONFLD FIELDNAME SHAPE.DATA
  CHARFLD FIELDNAME SHAPE.TYPE
    0 CONSTANT SHAPE.ELLIPSE
    1 CONSTANT SHAPE.TRIANGLE
    2 CONSTANT SHAPE.SQUARE
 STRUCTSIZE SHAPE.SIZE
 STRUCTALIGN SHAPE.ALIGN
 STRUCTEMBED SHAPE.FLD
;STRUCTURE

( shape -- data type )
: SHAPE.FIELDS DUP SHAPE.DATA SWAP SHAPE.TYPE ;

( [shape data] type shape -- )
: SHAPE.INIT
	SHAPE.FIELDS ROT DUP ROT C!
	CASE
		SHAPE.ELLIPSE  OF ELLIPSE.INIT  ENDOF
		SHAPE.TRIANGLE OF TRIANGLE.INIT ENDOF
		SHAPE.SQUARE   OF SQUARE.INIT   ENDOF
	ENDCASE
;

: SHAPE.PRINT 
	SHAPE.FIELDS
	C@ CASE
		SHAPE.ELLIPSE  OF ELLIPSE.PRINT  ENDOF
		SHAPE.TRIANGLE OF TRIANGLE.PRINT ENDOF
		SHAPE.SQUARE   OF SQUARE.PRINT   ENDOF
	ENDCASE
;

SHAPE.SIZE ALLOT HERE VARIABLE SHAPE

1 2 3 SHAPE.TRIANGLE SHAPE SHAPE.INIT
CR SHAPE SHAPE.PRINT
3 4 SHAPE.ELLIPSE SHAPE SHAPE.INIT
CR SHAPE SHAPE.PRINT
100 SHAPE.SQUARE SHAPE SHAPE.INIT
CR SHAPE SHAPE.PRINT
CR CR
